import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.cadixdev.licenser' version '0.6.1' apply false
    id 'org.ajoberstar.grgit' version '5.2.1'
    id 'checkstyle'
    id 'maven-publish'
    id 'java'
}

group 'net.william278'
version "$ext.plugin_version${versionMetadata()}"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'check', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()

    set 'jedis_version', jedis_version.toString()
    set 'sqlite_driver_version', sqlite_driver_version.toString()
    set 'mysql_driver_version', mysql_driver_version.toString()
    set 'mariadb_driver_version', mariadb_driver_version.toString()
}

allprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'checkstyle'
    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.release.set 17
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            mavenContent { snapshotsOnly() }
        }
        maven { url 'https://repo.william278.net/releases/' }
        maven { url 'https://maven.fabricmc.net/' }
        maven { url 'https://libraries.minecraft.net/' }
        maven { url 'https://api.modrinth.com/maven' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://mvn-repo.arim.space/lesser-gpl3/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url 'https://repo.minebench.de/' }
        maven { url 'https://repo.alessiodp.com/releases/' }
        maven { url 'https://repo.essentialsx.net/releases/' }
        maven { url 'https://repo.mikeprimm.com/' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    }

    license {
        header = rootProject.file('HEADER')
        include '**/*.java'
        newLine = true
    }

    checkstyle {
        toolVersion = '10.12.0'
    }

    processResources {
        filesMatching(['**/*.json', '**/*.yml']) {
            filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                    tokens: rootProject.ext.properties
        }
    }

}

subprojects {
    version rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name.capitalize()}"
    compileJava.options.encoding = 'UTF-8'

    jar {
        from '../LICENSE'
    }

    test {
        useJUnitPlatform()
    }

    if (['paper', 'fabric'].contains(project.name)) {
        compileJava.options.release.set 17
    }

    if (['bukkit', 'paper', 'plugin', 'fabric', 'sponge'].contains(project.name)) {
        shadowJar {
            destinationDirectory.set(file("$rootDir/target"))
            archiveClassifier.set('')
        }

        if (['bukkit'].contains(project.name)) {
            java {
                withSourcesJar()
                withJavadocJar()
            }
            sourcesJar {
                destinationDirectory.set(file("$rootDir/target"))
            }
            javadocJar {
                destinationDirectory.set(file("$rootDir/target"))
            }
            shadowJar.dependsOn(sourcesJar, javadocJar)

            publishing {
                repositories {
                    if (System.getenv("RELEASES_MAVEN_USERNAME") != null) {
                        maven {
                            name = "william278-releases"
                            url = "https://repo.william278.net/releases"
                            credentials {
                                username = System.getenv("RELEASES_MAVEN_USERNAME")
                                password = System.getenv("RELEASES_MAVEN_PASSWORD")
                            }
                            authentication {
                                basic(BasicAuthentication)
                            }
                        }
                    }
                    if (System.getenv("SNAPSHOTS_MAVEN_USERNAME") != null) {
                        maven {
                            name = "william278-snapshots"
                            url = "https://repo.william278.net/snapshots"
                            credentials {
                                username = System.getenv("SNAPSHOTS_MAVEN_USERNAME")
                                password = System.getenv("SNAPSHOTS_MAVEN_PASSWORD")
                            }
                            authentication {
                                basic(BasicAuthentication)
                            }
                        }
                    }
                }

                publications {
                    mavenJava(MavenPublication) {
                        groupId = 'net.william278'
                        artifactId = 'huskhomes'
                        version = "$rootProject.version"
                        artifact shadowJar
                        artifact javadocJar
                        artifact sourcesJar
                    }
                }
            }
        }

        jar.dependsOn shadowJar
        clean.delete "$rootDir/target"
    }
}

logger.lifecycle("Building HuskHomes ${version} by William278")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // Get if there is a tag for this commit
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }

    // Otherwise, get the last commit hash and if it's a clean head
    if (grgit == null) {
        return '-' + System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return '-' + grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}

