plugins {
    id 'gg.essential.multi-version'
    id 'gg.essential.defaults'
}

loom.serverOnlyMinecraftJar()

repositories {
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://maven.impactdev.net/repository/development/' }
    maven { url 'https://maven.nucleoid.xyz/' }
}

dependencies {
    modImplementation include("net.kyori:adventure-platform-fabric:${fabric_adventure_platform_version}")
    modImplementation include("me.lucko:fabric-permissions-api:${fabric_permissions_api_version}")
    modImplementation include("eu.pb4:placeholder-api:${fabric_placeholder_api_version}")
    modImplementation include("net.impactdev.impactor.api:economy:${fabric_impactor_api_version}")
    modImplementation include("net.william278.toilet:toilet-fabric:1.0.17+$project.name")
    modImplementation include("com.github.PokeSkies:FabricPluginMessaging:f621c4fcf6")
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    // Manually include config deps due to the way including api deps works
    implementation include("de.exlll:configlib-core:4.8.0")
    implementation include("org.snakeyaml:snakeyaml-engine:3.0.1")
    implementation include('org.apache.commons:commons-pool2:2.13.1')
    implementation include("redis.clients.authentication:redis-authx-core:0.1.1-beta2")

    // Include driver deps due to no runtime dep loading support
    implementation include("org.xerial:sqlite-jdbc:$sqlite_driver_version")
    implementation include("com.mysql:mysql-connector-j:$mysql_driver_version")
    implementation include("org.mariadb.jdbc:mariadb-java-client:$mariadb_driver_version")
    implementation include("redis.clients:jedis:$jedis_version")
    implementation include("com.h2database:h2:$h2_driver_version")

    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    compileOnly 'org.projectlombok:lombok:1.18.42'

    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    implementation include(project(path: ":common"))
    project(":common").configurations.api.dependencies.each { dependency ->
        include(dependency)
    }
}

processResources {
    filesMatching(Arrays.asList("fabric.mod.json")) {
        expand([
                version: version,
                fabric_loader_version: fabric_loader_version,
                fabric_minecraft_version: project.name,
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    destinationDirectory.set(file("$projectDir/build/libs"))

    exclude('net.fabricmc:.*')
    exclude('net.kyori:.*')
    exclude '/mappings/*'
}

remapJar {
    dependsOn tasks.shadowJar
    mustRunAfter tasks.shadowJar
    inputFile = shadowJar.archiveFile.get()
    addNestedDependencies = true

    destinationDirectory.set(file("$rootDir/target/"))
    archiveClassifier.set('')
}

shadowJar.finalizedBy(remapJar)
